namespace cruise.umple.sample.downloader.consistent;

class ImportFile {
  depend cruise.umple.compiler.UmpleImportType;
  depend cruise.umple.sample.downloader.ImportFSM;

  immutable String path;
  
  immutable UmpleImportType importType;
  
  internal String lastStage;
  
  immutable String message;
  
  ImportFSM.Action getLastState() {
    return ImportFSM.State.valueOf(lastStage);
  } 
  
  Boolean isSuccessful() {
    return getLastState() == ImportFSM.Action.Completed;
  }
}

class ImportRepository {
  depend cruise.umple.sample.downloader.DiagramType;
  depend cruise.umple.compiler.UmpleImportType;
  
  immutable String name;
  immutable String description;
  immutable String path;
  immutable DiagramType diagramType;
  defaulted Double successRate = 1.0;
  defaulted Double failRate = 0.0;
  
  1 -- * ImportFile files;
  
  after setSuccessRate {
    if (!Double.valueOf(getFailRate()).equals(1.0 - getSuccessRate())) {
      setFailRate(1.0 - getSuccessRate());
    }
  }
        
  after setFailRate {
    if (!Double.valueOf(getSuccessRate()).equals(1.0 - getFailRate())) {
      setSuccessRate(1.0 - getFailRate());
    }
  }
}

class ImportRepositorySet {
  depend cruise.umple.sample.downloader.DiagramType;
  
  immutable Date date;
  immutable Time time;
  
  immutable String umplePath;
  defaulted String srcPath = null;
  
  1 -- * ImportRepository repositories;

}